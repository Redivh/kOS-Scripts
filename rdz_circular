clearscreen.
for node in allnodes {remove node.}

set R1 to ship:orbit:semimajoraxis.
set R2 to target:orbit:semimajoraxis.
set a_trans to (R1+R2)/2.
set ecc_trans to abs(R2-R1)/(R1+R2).
lock T_ship to ship:orbit:period.
lock T_target to target:orbit:period.
set T_trans to 2*constant:pi*sqrt((a_trans^3)/ship:body:MU).
set Phase_burn to (T_trans/T_target)*180.
set Phase_ang_tol to 20.
lock Ship_TA to ship:orbit:trueanomaly.
lock Ship_ecc to ship:orbit:eccentricity.

lock Ship_angvel to VCRS(ship:velocity:orbit,(ship:position-ship:body:position)):normalized.
set Phase_ang to VANG(-1*ship:body:position,target:position-ship:body:position).
lock vec_check to VCRS(-1*ship:body:position,target:position-ship:body:position):normalized + Ship_angvel.

if vec_check:mag > 1 {
	set Phase_ang to 360-Phase_ang.
}

set Burn_TA to Ship_TA + (Phase_ang - (180-Phase_burn)).

if Burn_TA > 360 {
	set Burn_TA to Burn_TA - 360.
}
if Burn_TA < 0 {
	set Burn_TA to Burn_TA + 360.
}

set Ship_EA to 2*ARCTAN((TAN(Ship_TA/2))/sqrt((1+Ship_ecc)/(1-Ship_ecc))).
set Ship_MA to Ship_EA*constant:pi/180 - Ship_ecc*SIN(Ship_EA).
set Burn_EA to 2*ARCTAN((TAN(Burn_TA/2))/sqrt((1+Ship_ecc)/(1-Ship_ecc))).
set Burn_MA to Burn_EA*constant:pi/180 - Ship_ecc*SIN(Burn_EA).
set n to sqrt(ship:body:mu/(ship:orbit:semimajoraxis)^3).
set Burn_eta to (Burn_MA-Ship_MA)/n.

if Burn_eta < 0 {
	set Burn_eta to ship:orbit:period + Burn_eta.
}

set Delta_V to sqrt(ship:body:mu*(2/R1 - 1/a_trans))-sqrt(ship:body:mu/ship:orbit:semimajoraxis).
//set mynode to NODE(TIME:seconds + Burn_eta, 0, 0, Delta_V).
//ADD mynode.

print "Phase_burn = " + round(Phase_burn).
print "Phase_ang = " + round(Phase_ang).

set Phase_burn_test to 180-Phase_burn.
set Phase_dot to -1*360/T_ship + 360/T_target.
set T_burn to (Phase_burn_test - Phase_ang)/Phase_dot.
if T_burn < 0{
	if Phase_dot < 0{
		set T_burn to T_burn + T_ship.
	}
	else {
		set T_burn to (Phase_burn_test - Phase_ang + 360)/Phase_dot.
	}
}
print "T_burn = " + round(T_burn).
print "Burn_eta = " + round(Burn_eta).
print "Phase_dot = " + round(Phase_dot,2).
print "Phase_burn_test = " + round(Phase_burn_test).
set mynode to NODE(TIME:seconds + T_burn, 0, 0, Delta_V).
ADD mynode.